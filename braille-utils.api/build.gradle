apply plugin: 'java'
apply plugin: 'maven'

//custom plugins
apply plugin: "bundle"

compileJava.options.encoding = 'UTF-8'
compileTestJava.options.encoding = 'UTF-8'
javadoc {
	options.encoding = 'UTF-8'
	options.links('http://docs.oracle.com/javase/6/docs/api/');
}
sourceCompatibility = '1.6'
targetCompatibility = '1.6'

group = "org.daisy.braille"
version = "2.0-SNAPSHOT"

sourceSets {
	main {
		java {
			srcDir 'src'
		}
		resources {
			srcDir 'src'
		}
	}
	test {
		java {
			srcDir 'test'
		}
		resources {
			srcDir 'test'
		}
	}
}

jar {
	doFirst {
		manifest {
	    	attributes ('Built-By': '',				//'${user.name}'
	    	'Built-On': new Date(),					//'${ISO-TODAY}'
			'Repository-Revision': "$repositoryRevision",
			'Implementation-Title': "$releaseName Core",
			'Implementation-Version': "$releaseVersion",
			'Implementation-URL': 'http://code.google.com/p/brailleutils/',
			'Class-Path': configurations.compile.collect { it.getName() }.join(' ')					//'${core.classpath.attribute}'
			)
		}
	}
	from sourceSets.main.allJava
	baseName = 'brailleUtils-core'
}

repositories {
    mavenCentral()
    mavenLocal()
}

dependencies {
	//compile group: 'com.thaiopensource', name: 'jing', version: '20091111'
	compile group: 'org.daisy.libs', name: 'jing', version: '20120724.0.0'
	compile group: "biz.aQute.bnd", name: "annotation", version: "2.3.0"
	testCompile group: 'junit', name: 'junit', version: '4.7'
	runtime group: 'org.daisy.libs', name: 'saxon-he', version: '9.5.1.5'
}

buildscript {
    repositories { mavenCentral() }
    dependencies { classpath group: "org.dm.gradle", name: "gradle-bundle-plugin", version: "0.5" }
}

bundle {
    instructions << [
     	//must use file() to resolve against the build.gradle in Eclipse
        "-include": file('bnd.bnd'),
        'Repository-Revision': "$repositoryRevision"
    ]
}
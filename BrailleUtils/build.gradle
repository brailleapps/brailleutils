apply plugin: 'application'
apply plugin: 'maven'

mainClassName = "org.daisy.braille.ui.BasicUI"
startScripts.applicationName = 'brailleutils'

compileJava.options.encoding = 'UTF-8'
compileTestJava.options.encoding = 'UTF-8'
javadoc {
	options.encoding = 'UTF-8'
	options.links('http://docs.oracle.com/javase/6/docs/api/');
}

sourceCompatibility = '1.7'
targetCompatibility = '1.7'

group = "org.daisy.braille"

ext {
	versions = [
			'junit'		: '4.7',
			'osgi'		: '5.0.0',
			'felix'		: '4.4.1',
			'paxExam'	: '3.5.0', //4.4.0 requires Java 7
			'paxUrl'	: '2.3.0',
	]
}

sourceSets {
	main {	java { srcDir 'src' }	resources { srcDir 'src' } }
	test {
		java {
			srcDir 'test'
			srcDir 'integrationtest'
		}
		resources {
			srcDir 'test'
			srcDir 'integrationtest'
		}
	}
}

// required for gradle build outside of Eclipse
task copyTestResources(type: Copy) {
    from "${projectDir}/integrationtest"
    into "${buildDir}/classes/test"
}
processTestResources.dependsOn copyTestResources
//

jar.doFirst {
	manifest {
    	attributes ('Built-By': System.getProperty("user.name"),
    	'Main-Class': mainClassName,
		'Built-On': new Date().format('yyyy-MM-dd'),
		'Repository-Revision': "$repositoryRevision",
		'Implementation-Title': "$releaseName",
		'Implementation-Version': version,
		'Implementation-URL': "$repositoryURL",
		'Class-Path': configurations.compile.collect { it.getName() }.join(' ')			//'${core.classpath.attribute}'
		)
	}
	from sourceSets.main.allJava
	baseName = 'braille-utils.ui'
}

repositories {
	mavenCentral()
	//mavenLocal()
	//maven { url "https://oss.sonatype.org/content/groups/staging" }
}

dependencies {
	compile 'org.daisy.braille:braille-utils.api:2.0.0'
	compile 'org.daisy.braille:braille-utils.basic-cli:1.0.0'
	compile 'org.daisy.braille:braille-utils.pef-tools:1.0.0'
	
	runtime 'org.daisy.braille:braille-utils.impl:2.0.0'
	
	testCompile group: 'junit', name: 'junit', version: "$versions.junit"
	
	testCompile "javax.inject:javax.inject:1"
	testCompile "org.ops4j.pax.exam:pax-exam-junit4:$versions.paxExam"
	testRuntime "org.ops4j.pax.exam:pax-exam-container-native:$versions.paxExam"
	testRuntime "org.ops4j.pax.exam:pax-exam-link-mvn:$versions.paxExam"
	testRuntime "org.ops4j.pax.url:pax-url-aether:$versions.paxUrl"
	testRuntime "org.ops4j.pax.url:pax-url-reference:$versions.paxUrl"
	testRuntime "org.ops4j.pax.url:pax-url-wrap:$versions.paxUrl"
	testRuntime "org.apache.felix:org.apache.felix.framework:$versions.felix"
	testCompile("org.osgi:org.osgi.core:$versions.osgi")
	testCompile("org.osgi:org.osgi.compendium:$versions.osgi")
}

applicationDistribution.from(file('docs')) {
    into 'docs'
}
applicationDistribution.from(file('licenses')) {
	into 'licenses'
}
